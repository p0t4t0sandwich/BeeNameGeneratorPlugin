plugins {
    id('com.github.johnrengelman.shadow') version('8.1.1')
    id('net.neoforged.gradle') version('[6.0.18,6.2)')
    id('org.parchmentmc.librarian.forgegradle') version('1.+')
}

targetCompatibility = java_version

minecraft {
    mappings(channel: mappings_channel, version: mappings_version)
}

dependencies {
    implementation(project(path: ':common', configuration: 'shadow'))
    minecraft("net.neoforged:forge:${minecraft_version}-${neoforge_version}")

    // TaterLib
    compileOnly("dev.neuralnexus:TaterLib:1.20-${taterlib_version}")
}

reobf {
    shadowJar {}
}

shadowJar {
    dependencies {
        include(project(':common'))
    }

    destinationDirectory = file('../target')
    archiveFileName = "${project_name}-neoforge-${minecraft_version}-${version}.jar"
}
shadowJar.doLast {
    task ->
        ant.checksum(file: task.archivePath) {
            hashAlgorithm = 'md5'
            property = 'md5sum'
        }
}

artifacts {
    archives(shadowJar)
}
